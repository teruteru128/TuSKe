import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

java {
    def targetJava = 8
    targetCompatibility = JavaVersion.toVersion(targetJava)
    sourceCompatibility = JavaVersion.toVersion(targetJava)
}

repositories {
    jcenter()
    mavenCentral()
    //Spigot
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    //Skript
    maven {
        url 'https://raw.githubusercontent.com/bensku/mvn-repo/master'
    }
    //ViaVersion
    maven {
        url 'https://repo.viaversion.com/'
    }
    //Some Skript dependencies to avoid issues
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://mvn.yawk.at'
    }
    maven {
        url 'https://ci.emc.gs/nexus/content/groups/aikar/'
    }
    maven {
        url 'https://maven.elmakers.com/repository/'
    }
    //PreicousStone
    maven {
        url 'https://jitpack.io'
    }
}
configurations {
    compileWithJar
}
dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    //This is the default bukkit version supported. It is just for javadocs and jar hierarchy
    compileOnly 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
    //Since it uses NMS/OBC stuffs, it's required to manually add the bukkit jars.
    compileOnly fileTree(dir: 'dependencies', include: '*.jar')
    //Skript
    implementation 'com.github.SkriptLang:Skript:2.6-beta2'
    //Generex API. Used to expressions to generate strings from regex
    compileWithJar files('dependencies/Generex.jar')
    //ViaVersion
    compileOnly 'com.viaversion:viaversion-api:4.0.0'
    compileOnly 'com.viaversion:viaversion-bukkit:4.0.0'
//    configurations.compileOnly.extendsFrom(configurations.compileWithJar)
}

//Used to copy the final jar in test server located in at same place
//It requires that you execute "./gradlew clean build copy"
task copy(type: Copy) {
    from 'build/libs/TuSKe.jar'
    into 'server/plugins'
}

processResources {
    filter ReplaceTokens, tokens: [
            'version': project.property('version')
    ]
}

jar {
    archiveName 'TuSKe.jar'
    from {
        configurations.compileWithJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
}